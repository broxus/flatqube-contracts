pragma ever-solidity ^0.62.0;

import "../libraries/OrderStatus.tsol";
import "../libraries/PairPayload.tsol";
import "../libraries/OrderClosureType.tsol";
import "../libraries/DexOperationTypes.tsol";
import "../libraries/OrderOperationTypes.tsol";
import "../libraries/OrderOperationStatus.tsol";
import "../libraries/DexOperationStatusV2.tsol";

import "../structures/IOrderStructures.tsol";

library OrderPayloads {

    function buildOrderPayload(
        uint8 op,
        uint64 callbackId,
        uint128 deployWalletValue,
        address recipient,
        optional(TvmCell) successPayload,
        optional(TvmCell) cancelPayload
    ) public returns(TvmCell) {
        TvmBuilder builder;
        builder.store(op);
        builder.store(callbackId);
        builder.store(deployWalletValue);
        builder.store(recipient);

        if (successPayload.hasValue()) {
            builder.store(successPayload.get());
        }

        if (cancelPayload.hasValue()) {
            builder.store(cancelPayload.get());
        }

        return builder.toCell();
    }

    function buildSwapExchangePayload(
        uint64 _callbackId,
        uint128 _deployWalletValue,
        uint128 _amountSpent,
        uint128 _expectedAmount,
        address _owner,
        address _sender,
        address _recipient,
        address _outcoming,
        address _referrer
    ) public returns(TvmCell) {

        TvmBuilder successBuilder;
        successBuilder.store(DexOperationStatusV2.SUCCESS);
        successBuilder.store(_callbackId);
        successBuilder.store(_owner);
        successBuilder.store(_deployWalletValue);
        successBuilder.store(_amountSpent);

        optional(TvmCell) successPayload;
        successPayload.set(successBuilder.toCell());

        TvmBuilder cancelBuilder;
        cancelBuilder.store(DexOperationStatusV2.CANCEL);
        cancelBuilder.store(_callbackId);
        cancelBuilder.store(_sender);
        cancelBuilder.store(_amountSpent);

        optional(TvmCell) cancelPayload;
        cancelPayload.set(cancelBuilder.toCell());

        TvmCell pairPayload = PairPayload.buildExchangePayloadV2(
            _callbackId,
            _deployWalletValue,
            _expectedAmount,
            _recipient,
            _outcoming,
            _referrer,
            successPayload,
            cancelPayload
        );

        return pairPayload;
    }

    function buildMatchExchangePayload(
        uint8 op,
        uint64 callbackId,
        address sendGasTo,
        uint128 deployWalletValue,
        uint128 alienSpentAmount,
        uint128 expectedAmount,
        uint128 reward,
        address tokenReward
    ) public returns(TvmCell) {
        TvmBuilder builder;
        builder.store(op);
        builder.store(callbackId);
        builder.store(sendGasTo);
        builder.store(deployWalletValue);
        builder.store(alienSpentAmount);
        builder.store(expectedAmount);
        builder.store(reward);

        TvmBuilder builder2;
        builder2.store(tokenReward);

        builder.storeRef(builder2);

        return builder.toCell();
    }

    function buildMatchTransferPayload(
        uint8 op,
        uint64 callbackId,
        address sendGasTo,
        uint128 alienSpentAmount,
        uint128 expectedAmount,
        address owner
    ) public returns(TvmCell) {
        TvmBuilder builder;
        builder.store(op);
        builder.store(callbackId);
        builder.store(sendGasTo);
        builder.store(alienSpentAmount);
        builder.store(expectedAmount);
        builder.store(owner);

        return builder.toCell();
    }

    function decodeOnAcceptTokenTransferData (TvmCell _payload, uint8 state) public returns (
        bool,
        uint8,
        uint64,
        address,
        address,
        address,
        uint128,
        uint128,
        uint128,
        uint128,
        address
    ){
        TvmSlice payloadSlice = _payload.toSlice();
        if (state == OrderStatus.SwapInProgress && payloadSlice.bits() >= 16) {
            (uint8 payloadOperationType, uint8 _op) = payloadSlice.decode(uint8, uint8);
            if(
                _op == DexOperationTypes.EXCHANGE_V2 && payloadSlice.refs() >= 1 &&
                (
                    payloadOperationType == DexOperationStatusV2.SUCCESS ||
                    payloadOperationType == DexOperationStatusV2.CANCEL
                )
            ){
                payloadSlice = payloadSlice.loadRefAsSlice();
            }
        }

        bool isValid = payloadSlice.bits() >= 200;

        uint8 op;
        uint64 callbackId;
        address sendGasTo;
        address sendGasInMatching;
        address recipient;
        uint128 deployWalletValue;
        uint128 alienSpentAmount;
        uint128 expectedAmount;
        uint128 reward;
        address tokenReward;

        if (isValid) {
            if (state == OrderStatus.Initialize || state == OrderStatus.AwaitTokens) {
                op = OrderOperationTypes.CREATE_ORDER_IN_ORDER_ROOT;
                callbackId = payloadSlice.decode(uint64);
            } else {
                (
                    op,
                    callbackId
                ) = payloadSlice.decode(uint8, uint64);
            }

            if (
                payloadSlice.bits() >= 128 &&
                (op == OrderOperationTypes.EXCHANGE)
            ){
                deployWalletValue = payloadSlice.decode(uint128);
            }

            if (payloadSlice.bits() >= 267) {
                if (op != OrderOperationTypes.EXCHANGE){
                    sendGasTo = payloadSlice.decode(address);
                } else {
                    recipient = payloadSlice.decode(address);
                }
            }

            if (payloadSlice.bits() >= 128) {
                if (
                    op == OrderOperationTypes.MATCHING_ORDER_EXCHANGE ||
                    op == DexOperationStatusV2.SUCCESS
                ){
                    deployWalletValue = payloadSlice.decode(uint128);
                } else if (op == OrderOperationTypes.MATCHING_ORDER_TRANSFER){
                    alienSpentAmount = payloadSlice.decode(uint128);
                }
            }

            if (payloadSlice.bits() >= 128) {
                if (
                    op == OrderOperationTypes.MATCHING_ORDER_EXCHANGE ||
                    op == DexOperationStatusV2.SUCCESS
                ){
                    alienSpentAmount = payloadSlice.decode(uint128);
                } else if (
                    op == OrderOperationTypes.MATCHING_ORDER_TRANSFER ||
                    op == DexOperationStatusV2.CANCEL
                ){
                    expectedAmount = payloadSlice.decode(uint128);
                }
            }

            if (payloadSlice.bits() >= 256) {
                if (op == OrderOperationTypes.MATCHING_ORDER_EXCHANGE) {
                    (expectedAmount, reward) = payloadSlice.decode(uint128, uint128);
                    if (payloadSlice.refs() >= 1) {
                        TvmSlice refSlice = payloadSlice.loadRefAsSlice();
                        tokenReward = refSlice.decode(address);
                    }
                } else if (op == OrderOperationTypes.MATCHING_ORDER_TRANSFER){
                    sendGasInMatching = payloadSlice.decode(address);
                }
            }
        }

        return (
            isValid,
            op,
            callbackId,
            recipient,
            sendGasTo,
            sendGasInMatching,
            deployWalletValue,
            alienSpentAmount,
            expectedAmount,
            reward,
            tokenReward
        );
    }

    function decodeOnAcceptTokensTransferPayloads(TvmCell _payload, uint8 op) public returns (
    bool,
    TvmCell,
    bool,
    TvmCell
    ){
        TvmSlice slice = _payload.toSlice();
        uint8 refs = slice.refs();

        bool notifySuccess;
        bool notifyCancel;

        TvmCell successPayload;
        TvmCell cancelPayload;

        if (op != OrderOperationTypes.EXCHANGE || refs == 0) {
            return (notifySuccess, successPayload, notifyCancel, cancelPayload);
        }

        notifySuccess = refs >= 1;
        notifyCancel = refs >= 2;

        if (notifySuccess) { successPayload = slice.loadRef(); }
        if (notifyCancel) { cancelPayload = slice.loadRef(); }

        return (
            notifySuccess,
            successPayload,
            notifyCancel,
            cancelPayload
        );
    }

    function buildSuccessPayload(uint8 op, TvmCell originalPayload, address senderAddress) public returns (TvmCell) {

        TvmBuilder builder;

        builder.store(OrderOperationStatus.SUCCESS);
        builder.store(op);
        builder.store(originalPayload);

        TvmBuilder data;
        data.store(senderAddress);

        builder.store(data.toCell());

        return builder.toCell();
    }

    function buildCancelPayload(uint8 op, TvmCell originalPayload) public returns (TvmCell) {

        TvmBuilder builder;

        builder.store(OrderOperationStatus.CANCEL);
        builder.store(op);
        builder.store(originalPayload);

        return builder.toCell();

    }

    function buildCancelPayloadOrderRoot(uint8 op, uint16 errorCode, TvmCell originalPayload) public returns (TvmCell) {

        TvmBuilder builder;

        builder.store(OrderOperationStatus.CANCEL_ORDER_ROOT);
        builder.store(op);
        builder.store(errorCode);
        builder.store(originalPayload);

        return builder.toCell();

    }

    // Order operations payloads
    function buildOrderOperation(uint8 op, IOrderStructures.Details details, optional(uint128) amount) private returns (TvmCell){

        TvmBuilder builder;
        builder.store(op);

        optional(uint128) amount_opt;

        builder.storeRef(infoOrderBlock((amount.hasValue()? amount_opt : amount), details));
        builder.storeRef(initDataOrderBlock(details));

        return builder.toCell();
    }

    function buildOrderOperationFullExchange(IOrderStructures.Details details) public returns(TvmCell) {
        optional(uint128) amount;
        return buildOrderOperation(OrderClosureType.FULL_EXCHANGE, details, amount);
    }

    function buildOrderOperationChange(uint128 amount, IOrderStructures.Details details) public returns(TvmCell) {
        return buildOrderOperation(OrderClosureType.CHANGE, details, amount);
    }

    function buildOrderOperationPartExchange(uint128 amount, IOrderStructures.Details details) public returns(TvmCell) {
        return buildOrderOperation(OrderClosureType.PART_EXCHANGE, details, amount);
    }

    function buildOrderOperationAMM(uint128 amount, IOrderStructures.Details details) public returns(TvmCell) {
        return buildOrderOperation(OrderClosureType.AMM, details, amount);
    }

    function buildOrderOperationMatchingFull(IOrderStructures.Details details) public returns(TvmCell) {
        optional(uint128) amount;
        return buildOrderOperation(OrderClosureType.FULL_MATCHING, details, amount);
    }

    function buildOrderOperationMatchingPart(uint128 amount, IOrderStructures.Details details) public returns(TvmCell) {
        return buildOrderOperation(OrderClosureType.PART_EXCHANGE, details, amount);
    }

    function buildOrderOperationCancel(IOrderStructures.Details details) public returns(TvmCell) {
        optional(uint128) amount;
        return buildOrderOperation(OrderClosureType.CANCEL, details, amount);
    }

    function infoOrderBlock(optional(uint128) amount, IOrderStructures.Details details) private returns(TvmCell) {
        if (amount.hasValue()) {
            return abi.encode(
                amount.get(),
                details.initialAmount,
                details.expectedAmount,
                details.currentAmountSpentToken,
                details.currentAmountReceiveToken
            );
        } else {
            return abi.encode(
                details.initialAmount,
                details.expectedAmount,
                details.currentAmountSpentToken,
                details.currentAmountReceiveToken
            );
        }
    }

    function initDataOrderBlock(IOrderStructures.Details details) private returns(TvmCell) {
        return abi.encode(
            details.factory,
            details.root,
            details.owner,
            details.spentToken,
            details.receiveToken,
            details.timeTx,
            details.nowTx
        );
    }
}
